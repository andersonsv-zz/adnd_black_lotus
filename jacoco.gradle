apply plugin: "jacoco"

def exclude = [
        "**/R.class",
        '*/R$*.class',
        "**/BuildConfig.*",
        "*/Manifest*.*",
        "**/*Builder.*",
        "*/**Companion.*",
        '**/*$Lambda$*.*', // Jacoco can not handle several "$" in class name.
        '**/*$inlined$*.*', // Kotlin specific, Jacoco can not handle several "$" in class name.
]


task jacocoReport(type: JacocoReport) {
    group "Reporting"
    description "Generate Jacoco coverage reports."

    reports {
        xml.enabled = true
        html.enabled = true
    }
    // not including java srcSet since project is 100% kotlin
    sourceDirectories = files(["$project.projectDir/src/main/java"])
    classDirectories = (file("$project.buildDir/tmp/java-classes/debug").exists()
            ? fileTree(dir: "$project.buildDir/tmp/java-classes/debug", excludes: exclude)
            : fileTree(dir: "$project.buildDir/classes", excludes: exclude))

    // scans for both unit and instrumented test coverage results
    executionData = fileTree(dir: "$project.buildDir", includes: ["jacoco/*.exec", "outputs/code-coverage/connected/*.ec"])
}

afterEvaluate {
    tasks.findByName("check").dependsOn("jacocoReport")
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}